# ----------------------------------------------------------------------------------------
# --- Simple Snakemake snakefile toy example that maps reads and IDs variants
# ----------------------------------------------------------------------------------------

# This example Snakefile is based heavily off of (read: essentially plagiarized from)
#     the official Snakemake tutorial found at:
#     https://snakemake.readthedocs.io/en/stable/tutorial/basics.html

# It uses a tiny subset of the genomes of Ugandan mosquitoes generated by the
#     Ag1000G project: https://www.malariagen.net/data/ag1000g-phase3-snp

# Specifically, it maps the reads near the Vgsc/kdr insecticide-related locus
#    to the AgamP4 genome (using just chromosome arm 2L) and calls variants.

# The example dataset can be generated with scripts/generate_sample_dataset.sh

# Load sample IDs
configfile: "config.yaml"

# --- Final output file is VCF of variant calls

rule all:
    input:
        "results/variant_calls/all.raw.vcf"

# --- Map reads to reference genome (actually just chromosome arm 2L)

rule bwa_map:
    input:
        config["genome_fa"],
        "data/{sample}.R1.fastq",
        "data/{sample}.R2.fastq",
    output:
        "results/mapped_reads/{sample}.bam"
    shell:
        "module load bwa; module load samtools; " # Needed for Rutgers cluster
        "mkdir -p results/mapped_reads; "
        "bwa mem {input} | samtools view -Sb - > {output}"

# --- Sort BAM alignment files

rule samtools_sort:
    input:
        "results/mapped_reads/{sample}.bam"
    output:
        "results/sorted_reads/{sample}.bam"
    shell:
        "module load samtools/1.2; " # Needed for Rutgers cluster
        "mkdir -p results/sorted_reads; "
        "samtools sort -T results/sorted_reads/{wildcards.sample} "
        "    -O bam {input} > {output}"

# --- Index BAM alignment files

rule samtools_index:
    input:
        "results/sorted_reads/{sample}.bam"
    output:
        "results/sorted_reads/{sample}.bam.bai"
    shell:
        "module load samtools/1.2; " # Needed for Rutgers cluster
        "samtools index {input}"

# --- Call variants (SNPs and indels) in a quick and dirty way

rule bcftools_call:
    input:
        fa=config["genome_fa"],
        bam=expand("results/sorted_reads/{sample}.bam",
            sample=config["sample_ids"]),
        bai=expand("results/sorted_reads/{sample}.bam.bai",
            sample=config["sample_ids"])
    output:
        "results/variant_calls/all.raw.vcf"
    shell:
        "module load samtools/1.2; module load bcftools; " # Needed for Rutgers cluster
        "mkdir -p results/variant_calls; "
        "samtools mpileup -g -f {input.fa} {input.bam} | "
        "bcftools call -mv - > {output}"
